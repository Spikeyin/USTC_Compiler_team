## B2 Mem2Reg

#### B2-1

`Mem2Reg`优化遍的流程如下：
1. 对每个函数执行相同的优化遍。
2. 判断当前所处函数中是否有基本块，如果没有则跳过，若有则先进行变量初始化（`lvalue_connection.clear()`，`no_union_set.clear()`）。
3. `insideBlockForwarding()`：遍历当前函数的每个基本块，对每一个基本块进行优化。
  - 遍历基本块中的每一条指令，如果当前指令不只涉及局部变量，则直接跳过即可。
  - `store` ：检查当前指令是否已在 `forward_list` 中，如果在，则找到对应的 `rvalue`；检查当前指令的 `lvalue` 是否已在 `defined_list` 中，根据情况决定是否插入 `delete_list` 或 `defined_list`；检查当前指令的 `lvalue` 是否已在 `new_value` 中，并更新 `new_value` 。
  - `load` ：检查当前指令的 `lvalue` 是否已在`defined_list`中，如果在，则在 `new_value` 中找到新值，并将`inst-new_value`对加入 `forward_list`。
  - 遍历 `forward_list`，对每一个`inst-value`对，对该 `inst` 的 `use_list`遍历，将所有可以被替换的操作数替换为 `value`。
  - 删除无用的 `load` 和 `store`。
4. `genPhi()`，找到多个基本块的共同活跃变量集合，对每个名字，分别插入 $\phi$ 指令。
5. `valueDefineCounting()`：对于需要重命名的变量进行计数，然后按序添加下标，从而得到SSA格式的代码。
6. `valueForwarding()`：变量重命名算法。
  - 遍历当前基本块中的每一条$\phi$指令，对应修改`value_status`（`Value`的一对多映射）。
  - 对于所有的 `load` 指令涉及的`lvalue`，对所有的`use`都替换成最新的`new_value`；对于所有的 `store` 指令，对应更新 `value_status`，删除无用指令。
  - 遍历当前基本块的所有后继基本块，处理$\phi$指令，填入相应的操作数，并继续对后继基本块递归调用`valueForwarding`，完成块间优化。
  - 对于当前基本块定义的变量，依次对`value_status`中该变量对应的栈做一次弹出。
  - 删除无用指令。
7. `removeAlloc`：删除除数组元素之外的 `alloca` 指令。

#### B2-2

我使用的`SysYF`程序如下:
```c++
int main(){
    int a1;
    int a2;
    a1 = 2;
    a2 = -2;
    while (a1 <= 4){
        if (a1 == 2){
            a2 = a1 + a1;
            a1 = a1 + 2;
        }
        else {
            a1 = a1 + 1;
        }
    }
    putint(a2);putint(a1);
	return 0;
}
```

**不开优化时产生的`.ll`**：

```c++
declare i32 @get_int()

declare float @get_float()

declare i32 @get_char()

declare i32 @get_int_array(i32*)

declare i32 @get_float_array(float*)

declare void @put_int(i32)

declare void @put_float(float)

declare void @put_char(i32)

declare void @put_int_array(i32, i32*)

declare void @put_float_array(i32, float*)

define i32 @main() {
label_entry:
  %op0 = alloca i32
  %op1 = alloca i32
  %op2 = alloca i32
  store i32 2, i32* %op1
  store i32 -2, i32* %op2
  br label %label4
label_ret:                                                ; preds = %label14
  %op3 = load i32, i32* %op0
  ret i32 %op3
label4:                                                ; preds = %label_entry, %label26
  %op5 = load i32, i32* %op1
  %op6 = icmp sle i32 %op5, 4
  %op7 = zext i1 %op6 to i32
  %op8 = icmp ne i32 %op7, 0
  br i1 %op8, label %label9, label %label14
label9:                                                ; preds = %label4
  %op10 = load i32, i32* %op1
  %op11 = icmp eq i32 %op10, 2
  %op12 = zext i1 %op11 to i32
  %op13 = icmp ne i32 %op12, 0
  br i1 %op13, label %label17, label %label23
label14:                                                ; preds = %label4
  %op15 = load i32, i32* %op2
  call void @put_int(i32 %op15)
  %op16 = load i32, i32* %op1
  call void @put_int(i32 %op16)
  store i32 0, i32* %op0
  br label %label_ret
label17:                                                ; preds = %label9
  %op18 = load i32, i32* %op1
  %op19 = load i32, i32* %op1
  %op20 = add i32 %op18, %op19
  store i32 %op20, i32* %op2
  %op21 = load i32, i32* %op1
  %op22 = add i32 %op21, 2
  store i32 %op22, i32* %op1
  br label %label26
label23:                                                ; preds = %label9
  %op24 = load i32, i32* %op1
  %op25 = add i32 %op24, 1
  store i32 %op25, i32* %op1
  br label %label26
label26:                                                ; preds = %label17, %label23
  br label %label4
}

```

**开启`-O`选项后产生的`.ll`**：

```c++
declare i32 @get_int()

declare float @get_float()

declare i32 @get_char()

declare i32 @get_int_array(i32*)

declare i32 @get_float_array(float*)

declare void @put_int(i32)

declare void @put_float(float)

declare void @put_char(i32)

declare void @put_int_array(i32, i32*)

declare void @put_float_array(i32, float*)

define i32 @main() {
label_entry:
  br label %label4
label_ret:                                                ; preds = %label14
  ret i32 0
label4:                                                ; preds = %label_entry, %label26
  %op27 = phi i32 [ -2, %label_entry ], [ %op29, %label26 ]
  %op28 = phi i32 [ 2, %label_entry ], [ %op30, %label26 ]
  %op6 = icmp sle i32 %op28, 4
  %op7 = zext i1 %op6 to i32
  %op8 = icmp ne i32 %op7, 0
  br i1 %op8, label %label9, label %label14
label9:                                                ; preds = %label4
  %op11 = icmp eq i32 %op28, 2
  %op12 = zext i1 %op11 to i32
  %op13 = icmp ne i32 %op12, 0
  br i1 %op13, label %label17, label %label23
label14:                                                ; preds = %label4
  call void @put_int(i32 %op27)
  call void @put_int(i32 %op28)
  br label %label_ret
label17:                                                ; preds = %label9
  %op20 = add i32 %op28, %op28
  %op22 = add i32 %op28, 2
  br label %label26
label23:                                                ; preds = %label9
  %op25 = add i32 %op28, 1
  br label %label26
label26:                                                ; preds = %label17, %label23
  %op29 = phi i32 [ %op27, %label23 ], [ %op20, %label17 ]
  %op30 = phi i32 [ %op25, %label23 ], [ %op22, %label17 ]
  br label %label4
}
```

**问题回答**：

- `Mem2Reg`可能会删除的指令类型是哪些？对哪些分配(alloca)指令会有影响？
答：
1. 部分`load` 和 `store` 指令；所有除 `array` 类型的 `alloca` 指令。
2. 除了使用`array`的 `alloca` 之外的所有 `alloca` 指令都会被删除。

- 在基本块内前进`insideBlockForwarding`时，对`store`指令处理时为什么`rvalue`在`forward_list`中存在时，就需要将`rvalue`替换成`forward_list`映射中的`->second`值？
答：`rvalue` 在 `forward_list` 中存在时，说明之前已经处理过某条 `load`指令，更新了某个已在`defined_list` 里的变量。这时进行替换，是为了把此处 `store` 指令中的值更新成最新的值。

- 在基本块内前进时，`defined_list`代表什么含义？
答：
`defined_list` 是 `Ptr<Value>` 到 `Ptr<Instruction>` 的`map`，它记录了当前基本块中对某个 `lvalue` 进行定值的最新一条指令。

- 生成phi指令`genPhi`的第一步两层for循环在收集什么信息，这些信息在后面的循环中如何被利用生成Phi指令？
答：
两层循环收集了当前函数中的全局名字集合（即 `PtrSet<Value> globals`）以及在基本块中进行定值的`Map<Ptr<Value>, PtrSet<BasicBlock>`。在插入$\phi$指令时时，此`map`就是初始化的工作列表，对其中每个基本块，在其支配的边界基本块起始处对应地插入$\phi$指令。

- `valueDefineCounting`为`defined_var`记录了什么信息？
答：
`valueDefineCounting` 函数将$\phi$指令和 `store` 均视为对变量的定值，对应的`lvalue`均加入`defined_var`中该基本块对应的`Set`。

- `valueForwarding`在遍历基本块时采用的什么方式？
答：
递归调用的方式，即先遍历当前`BB`的后继`BB`，进行$\phi$指令的处理。然后重复遍历，之后继续对其在支配树上的每一个后继`BB`递归调用`valueForwarding`。
```c++
for(auto succbb: bb->get_succ_basic_blocks()){
    if(visited.find(succbb.lock())!=visited.end())continue;
    valueForwarding(succbb.lock());
}
```

- `valueForwarding`中为什么`value_status`需要对phi指令做信息收集？
答：
相当于对$\phi$指令涉及到的变量改名，如果不收集这些信息，则若某个基本块中某个变量的值来自不同分支里的不同基本块，那么后续产生$\phi$指令后整合产生的新名字就无法被优化。

- `valueForwarding`中第二个循环对`load`指令的替换是什么含义？
答：
对该条指令`load` 的目标变量 `lvalue` ，找到执行到这条指令时目标变量实际的值，即`new_value`，并用`new_value`对该 `lvalue` 的所有引用地方进行替换，如下面代码所示：
```c++
if(inst->get_instr_type() == Instruction::OpID::load){
    auto lvalue = static_pointer_cast<LoadInst>(inst)->get_lval();
    auto new_value = *(value_status.find(lvalue)->second.end() - 1);
    inst->replace_all_use_with(new_value);
}
```

- `valueForwarding`中出现的`defined_var`和`value_status`插入条目之间有什么联系？
答：
由下面代码可知，在处理完某一个基本块中的指令后，`define_var`中的条目正好是`value_status`中的栈顶条目。
```c++
auto var_set = define_var.find(bb)->second;
for(auto var: var_set){
    if(value_status.find(var.lock()) == value_status.end())continue;
    if(value_status.find(var.lock())->second.size() == 0)continue;
    value_status.find(var.lock())->second.pop_back();
}
```

#### B2-3
上面的`SysYF`验证代码如下：
```c++
int main(){
    int a1;
    int a2;
    a1 = 2;
    a2 = -2;
    while (a1 <= 4){
        if (a1 == 2){
            a2 = a1 + a1;
            a1 = a1 + 2;
        }
        else {
            a1 = a1 + 1;
        }
    }
    putint(a2);putint(a1);
	return 0;
}
```
**为什么要使用两层嵌套结构**：
+ 在 `if` 分支中，有对 `a1, a2` 两个变量的定值和引用，所以优化后产生了$\phi$指令。
+ 双层嵌套时生成的$\phi$指令共有4条，由上面`-O`后产生的`.ll`代码可知。
所以该程序会涉及支配树上的多层结构，因此能够反映这些$\phi$指令之间的流图依赖性，所以这种例子能体现`Mem2Reg`的效果。